apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.darryncampbell.productflavours"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        emdk6 {
            targetSdkVersion 23
            minSdkVersion 19    //  EMDK 6.0 supports as far back as KitKat (19)
            versionCode 30000 + android.defaultConfig.versionCode
            versionNameSuffix "-emdk6"
        }
        emdk5 {
            //  Not compatible with Marshmallow devices
            minSdkVersion 16
            targetSdkVersion 16  //  Though EMDK 5 actually supported up to Lollipop, we are only using it for JB deployments
            maxSdkVersion 22 //  Note: Google would still allow installation on 23+ devices (https://developer.android.com/guide/topics/manifest/uses-sdk-element.html)
            versionCode 75000 + android.defaultConfig.versionCode
            versionNameSuffix "-emdk5"
        }
    }

 }

dependencies {
    //  NOTE: This gradle depends on the EMDK Jars associated with EMDK 5 & 6 being in the following directories
    emdk5Provided fileTree(include: ['com.symbol.emdk.jar'], dir: 'lib\\emdk5.0')
    emdk6Provided fileTree(include: ['com.symbol.emdk.jar'], dir: 'lib\\emdk6.0')
    compile fileTree(exclude: ['com.symbol.emdk.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.0'
}
